//apply plugin: 'com.android.library'

plugins {
    id 'com.android.library'
}

ext {
    PUBLISH_GROUP_ID = 'com.zebra.printwrapper'
    PUBLISH_ARTIFACT_ID = 'PrintWrapper'
    PUBLISH_VERSION = '1.2.0'
}

android {
    namespace 'com.zebra.printwrapper'

    ndkVersion "21.0.6113669"
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 29
        targetSdkVersion 34
        versionCode 12
        versionName "1.2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14"
            }
        }
        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix " - Debug"
            debuggable true
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations.all {
    resolutionStrategy.force "org.jetbrains.kotlin:kotlin-stdlib:1.8.22"
    resolutionStrategy.force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.22"
}


dependencies {
    //implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.22"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.22"

    implementation files('libs/ZSDK_ANDROID_API.jar')
    implementation files('libs/jackson-core-2.2.3.jar')
    implementation files('libs/jackson-annotations-2.2.3.jar')
    implementation files('libs/jackson-databind-2.2.3.jar')
    testImplementation 'junit:junit:4.12'
    implementation 'com.tom-roush:pdfbox-android:2.0.27.0'

}

def jarName = 'PrintWrapper.jar'

task clearJarRelease(type: Delete) {
    delete "${project.buildDir}/outputs/jar/release/" + jarName
}

task clearJarDebug(type: Delete) {
    delete "${project.buildDir}/outputs/jar/debug/" + jarName
}

task makeJarRelease(type: Copy) {
    from("${project.buildDir}/intermediates/intermediates/aar_main_jar/release/")
    into("${project.buildDir}/outputs/jar/release")
    include('classes.jar')
    rename('classes.jar', jarName)
}

task makeJarDebug(type: Copy) {
    from("${project.buildDir}/intermediates/intermediates/aar_main_jar/debug/")
    into("${project.buildDir}/outputs/jar/debug")
    include('classes.jar')
    rename('classes.jar', jarName)
}

task makeAllJar(type: Copy) {}

makeJarRelease.dependsOn(clearJarRelease, build)
makeJarDebug.dependsOn(clearJarDebug, build)

makeAllJar.dependsOn(makeJarDebug,makeJarRelease)

/*
Publish tasks
https://medium.com/@daniellevass/how-to-publish-your-android-studio-library-to-jcenter-5384172c4739
https://raw.githubusercontent.com/blundell/release-android-library/master/android-release-aar.gradle
 */
